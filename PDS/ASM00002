//JOBNAME JOB (ACCT,'ROOM-NBR'),'HOTTEL PRIMES',MSGCLASS=S,CLASS=K,
//  TIME=(,30)
//PROCLIB JCLLIB ORDER=USERID.DVL.PROC
//        SET TITLE1='XXX.ASM00002'
//OUTPUT INCLUDE MEMBER=DESTCH
//*-------------------------------------------------------------------*
//*        ASSEMBLY                                                   *
//*-------------------------------------------------------------------*
//STEP02   EXEC HLASMCLG
//C.SYSLIB DD DSN=USERID.MACLIB,DISP=SHR
//         DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSUT2 DD DSN=&&SYSUT2,UNIT=SYSDA,
//           SPACE=(1700,(600,100))
//C.SYSPUNCH DD DUMMY
//C.SYSIN DD *
         TITLE 'PRIMES'
         PRINT NOGEN
*---------------------------------------------------------------------*
*        18 JAN 1980 - CHARLES HOTTEL                                 *
*        THIS PROGRAM CALULATES AND PRINTS ALL OF THE PRIME NUMBERS   *
*        LESS THAN NMAX, BASED ON ALOGORITHM P IN DONALD KNUTH'S      *
*        THE ART OF COMPUTER PROGRAMMING, FUNDAMENTAL ALGORITHMS,     *
*        2ND EDITION. PAGES 143-144. USES BINARY ARITHMETIC.          *
*---------------------------------------------------------------------*
PRIMES   START
         STM   R14,R12,12(R13)
         BALR  R15,0
         USING *,R15
         L     R1,MAIN-16
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R12,MAIN-12
         DROP  R15
         USING MAIN,R10,R11,R12
         B     MAIN
         DC    A(SAVE,MAIN,MAIN+4096,MAIN+8192)
*---------------------------------------------------------------------*
*   MAIN LINE CODE                                                    *
*---------------------------------------------------------------------*
MAIN     EQU   *
         LA    R3,PRIMETBL+4     R3 PTR TO NEXT SLOT TO STORE PRIME
         SR    R2,R2             ZERO OUT N
         LA    R2,3              INITIALIZE N
NPRIME   ST    R2,0(R3)          PUT PRIME IN TABLE
         LA    R3,4(R3)          POINT TO NEXT SLOT IN TABLE
         CL    R3,=A(TBLEND)     TABLE CAPACITY EXCEEDED?
         BL    NEXTN             NO
         ABEND 001,DUMP          YES
NEXTN    A     R2,=F'2'
         CL    R2,NMAX           MAXIMUM N?
         BH    PRINTRTN          YES
         LA    R4,PRIMETBL+4     NO, R4 PTR TO PRIME DIVISORS
PRIMETST SR    R6,R6
         LR    R7,R2             N IN R7
         D     R6,0(R4)          DIVIDE N BY PREVIOUS PRIME
         CL    R6,=F'0'          REMAINDER ZERO?
         BE    NEXTN             YES, N NOT PRIME
         CL    R7,0(R4)          QUOTIENT LESS THAN OR EQUAL PREV PRIME
         BNH   NPRIME            YES, N IS PRIME
         LA    R4,4(R4)          NEXT PRIME DIVISOR
         B     PRIMETST
PRINTRTN EQU   *
         OPEN  (PRINTER,OUTPUT)
         LA    R5,=A(SLOT1)      PTR TO FIRST SLOT IN PRINT LINE
         LA    R4,PRIMETBL       PTR TO 1ST PRIME IN TABLE
TBLENDCK CR    R3,R4             ANY PRIMES LEFT IN TABLE?
         BNH   LASTLINE          NONE LEFT
PGBREAK  CP    LINECNT,=P'53'    END OF PAGE?
         BL    LINEFILL          NO
         AP    PAGECNT,=P'1'     YES
         MVC   PAGENBR,PGMASK
         ED    PAGENBR,PAGECNT
         PUT   PRINTER,TITLE
         PUT   PRINTER,BLANKS
         ZAP   LINECNT,=P'2'
LINEFILL CL    R5,=A(SLOT10)     READY TO PRINT LINE?
         BNH   SLOTFILL          NO
         PUT   PRINTER,DETAIL    YES
         AP    LINECNT,=P'1'
         LA    R5,SLOT1
         B     PGBREAK
SLOTFILL MVC   0(8,R5),PRTMASK
         L     R7,0(R4)          PUT PRIME IN R7
         CVD   R7,DBLWORD        CONVERT PRIME TO PACKED DECIMAL
         ED    0(8,R5),DBLWORD+4
         LA    R5,12(R5)         POINT TO NEXT SLOT
         LA    R4,4(R4)          POINT TO NEXT SLOT IN PRIME TABLE
         B     TBLENDCK
LASTLINE CL    R5,=A(SLOT1)      PRINT PARTIAL LINE?
         BNH   CLOSERTN          NO
LASTSLOT MVC   0(8,R5),=8C' '    YES, BLANK OUT SLOT
         CL    R5,=A(SLOT10)     LAST SLOT?
         LA    R5,12(R5)         POINT TO NEXT SLOT TO BE BLANKED OUT
         BL    LASTSLOT          NO
         PUT   PRINTER,DETAIL    YES
CLOSERTN CLOSE (PRINTER)
MAIN99   SR    R15,R15
         B     SEXIT
         TITLE 'COMMON SUBROUTINE ENTRY/EXIT'
*
*   COMMON SUBROUTINE ENTRY
*
SENTER   STM   R14,R12,12(R13)
         C     R13,=A(SAVEND)
         BL    SENTER1
         DC    H'0'
         DC    C'SAVE AREA OVERFLOW'
SENTER1  LR    R9,R15
         LA    R15,72(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         BR    R9
*
*   COMMON SUBROUTINE EXIT
*
SEXIT    L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'
         BR    R14
         TITLE 'REGISTER EQUATES AND USAGE'
*
*   REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9   SENTER BRANCH TO SUBROUTINE, SUBROUTINE USES AS BASE
R10      EQU   10  BASE REGISTER FOR MAIN
R11      EQU   11  BASE REGISTER FOR MAIN
R12      EQU   12  BASE REGISTER FOR MAIN
R13      EQU   13  SAVE AREA ADDRESS
R14      EQU   14  SEXIT RETURN ADDRESS
R15      EQU   15  BRANCH TO ADDRESS, SENTER COPIES TO R9
*                  RETURN CODE FROM SUBROUTINE
         TITLE 'MAIN ROUTINE DATA'
NMAX     DC    F'+99999'         MAXIMUM N
*
DBLWORD  DS    D
*
PAGECNT  DC    PL2'0'
LINECNT  DC    PL2'53'
*
         EJECT
TITLE    DS    0CL133
         DC    CL1'1'
         DC    CL60' '
         DC    CL12' P R I M E S '
         DC    CL52' '
         DC    CL4'PAGE'
PAGENBR  DC    CL4'0'
*
PGMASK   DC    XL4'40202120'
*
BLANKS   DC    CL133' '
*
PRTMASK  DC    XL8'4020202020202120'
*
DETAIL   DS    0CL133
         DC    CL1'1'
         DC    CL8' '
SLOT1    DS    CL8
         DC    CL4' '
SLOT2    DS    CL8
         DC    CL4' '
SLOT3    DS    CL8
         DC    CL4' '
SLOT4    DS    CL8
         DC    CL4' '
SLOT5    DS    CL8
         DC    CL4' '
SLOT6    DS    CL8
         DC    CL4' '
SLOT7    DS    CL8
         DC    CL4' '
SLOT8    DS    CL8
         DC    CL4' '
SLOT9    DS    CL8
         DC    CL4' '
SLOT10   DS    CL9
         DC    CL8' '
*
         EJECT
PRINTER  DCB   DSORG=PS,                                               X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               BLKSIZE=27930,                                          X
               LRECL=133,                                              X
               DDNAME=PRINTER
         EJECT
         LTORG
         EJECT
PRIMETBL EQU   *
         DC    PL4'2'
         DS    16000F
         DS    16000F
         DS    16000F
TBLEND   DS    CL1
PRIMES1  CSECT
*
*   REMOTE AREAS
*
SAVE     DS    90D
SAVEND   EQU   *
         END
//G.PRINTER  DD SYSOUT=*
//G.SYSOUT   DD SYSOUT=*
//G.SYSUDUMP DD SYSOUT=*
