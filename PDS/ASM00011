//JOBNAME JOB (ACCT,'ROOM-NBR'),'HOTTEL SORTKEYS',MSGCLASS=S,CLASS=K,
//  TIME=(,30)
//PROCLIB JCLLIB ORDER=USERID.DVL.PROC
//        SET TITLE1='XXX.ASM00011'
//OUTPUT INCLUDE MEMBER=DESTCH
//*-------------------------------------------------------------------*
//*        ASSEMBLY                                                   *
//*-------------------------------------------------------------------*
//STEP02   EXEC HLASMCLG
//C.SYSLIB DD DSN=USERID.MACLIB,DISP=SHR
//         DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSUT2 DD DSN=&&SYSUT2,UNIT=SYSDA,
//           SPACE=(1700,(600,100))
//C.SYSPUNCH DD DUMMY
//C.SYSIN DD *
         TITLE 'GENERATE 1000 10 DIGIT SORT KEYS'
         PRINT NOGEN
*---------------------------------------------------------------------*
*        18 FEB 1983 - CHARLES HOTTEL                                 *
*        KNUTH, SORTING AND SEARCHING,  2ND EDITION, PP 382           *
*                                                                     *
*        K(1001) = ZERO                                               *
*        K(N-1)  = (3141592621 * K(N) + 2113148651) MOD 10**10        *
*                = (         M * K(N) +          A) MOD 10**10        *
*---------------------------------------------------------------------*
SORTKEYS START
         STM   R14,R12,12(R13)
         BALR  R15,0
         USING *,R15
         L     R1,MAIN-12
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R11,MAIN-8
         DROP  R15
         USING MAIN,R10,R11
         B     MAIN
         DC    A(SAVE,MAIN,MAIN+4096)
*---------------------------------------------------------------------*
*   MAIN LINE CODE                                                    *
*---------------------------------------------------------------------*
MAIN     EQU   *
         OPEN  (PRINTER,OUTPUT)
         L     R2,=A(K1001)        POINT TO K(N)
         LR    R3,R2
         S     R3,=F'6'            POINT TO K(N-1)
         LA    R4,1000             NUMBER OF K TO GENERATE
LOOP     EQU   *
         XC    WORK,WORK           CLEAR WORK AREA
         MVC   WORK+6(6),0(R2)     K(N)
         MP    WORK,M              M * K(N)
         AP    WORK,A              M * K(N) + A
         MVC   0(6,R3),WORK+6
         NI    0(R3),X'0F'         MOD 10**10
         UNPK  KEY,0(6,R3)
         OI    KEY+9,X'F0'         FIX SIGN
         PUT   PRINTER,OUTREC
         S     R2,=F'6'
         S     R3,=F'6'
         BCT   R4,LOOP             BRANCH IF NOT 1000 YET
         CLOSE (PRINTER)
MAIN99   SR    R15,R15
         B     SEXIT
         TITLE 'COMMON SUBROUTINE ENTRY/EXIT'
*
*   COMMON SUBROUTINE ENTRY
*
SENTER   STM   R14,R12,12(R13)
         C     R13,=A(SAVEND)
         BL    SENTER1
         DC    H'0'
         DC    C'SAVE AREA OVERFLOW'
SENTER1  LR    R9,R15
         LA    R15,72(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         BR    R9
*
*   COMMON SUBROUTINE EXIT
*
SEXIT    L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'
         BR    R14
         TITLE 'REGISTER EQUATES AND USAGE'
*
*   REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9   SENTER BRANCH TO SUBROUTINE, SUBROUTINE USES AS BASE
R10      EQU   10  BASE REGISTER FOR MAIN
R11      EQU   11  BASE REGISTER FOR MAIN
R12      EQU   12  BASE REGISTER FOR MAIN
R13      EQU   13  SAVE AREA ADDRESS
R14      EQU   14  SEXIT RETURN ADDRESS
R15      EQU   15  BRANCH TO ADDRESS, SENTER COPIES TO R9
*                  RETURN CODE FROM SUBROUTINE
         TITLE 'LITERALS'
         LTORG
         TITLE 'DATA'
         DS    0D
OUTREC   DS    0CL80
KEY      DS    CL10
         DC    70CL1' '
PRINTER  DCB   DSORG=PS,RECFM=FB,BLKSIZE=27920,LRECL=80,               X
               DDNAME=PRINTER,MACRF=PM
         DS    0F
         TITLE 'DATA AREAS'
WORK     DS PL12
A        DC    PL6'2113148651'
M        DC    PL6'3141592621'
K        DS    1000PL6
K1001    DC    PL6'0'
SORTKEY1 CSECT
*
*   REMOTE AREAS
*
SAVE     DS    90D
SAVEND   EQU   *
         END   SORTKEYS
//G.PRINTER  DD SYSOUT=*
//G.SYSOUT   DD SYSOUT=*
//G.SYSUDUMP DD SYSOUT=*
