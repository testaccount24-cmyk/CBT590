       IDENTIFICATION DIVISION.
         PROGRAM-ID. COB2SYS.
         AUTHOR. GILBERT SAINT-FLOUR <GSF@IBM.NET>.
      *----------------------------------------------------------------
      *
      *    THIS PROGRAM RETRIEVES SPECIFIC SYSTEM-RELATED DATA FROM
      *    MVS CONTROL BLOCKS AND MOVES IT TO WORKING-STORAGE.
      *
      *    THE NAME OF THE CONTROL-BLOCK IS INDICATED IN POS 1-6 OF
      *    THE PROCEDURE DIVISION LINES.
      *    THE LAYOUT OF THE MVS CONTROL BLOCKS IS DESCRIBED IN THE
      *    MVS DATA AREAS MANUALS, WHICH CAN BE FOUND ON ANY MVS OR
      *    OS/390 CD COLLECTION OR VIEWED ON-LINE BY GOING TO:
      *        HTTP://WWW.S390.IBM.COM/BOOKMGR-CGI/BOOKMGR.CMD/LIBRARY
      *    AND SEARCHING FOR:
      *        MVS DATA AREAS
      *
      *----------------------------------------------------------------
      /
       DATA DIVISION.
        WORKING-STORAGE SECTION.
         01 RESULTS.
           05 SYS-NAME                      PIC X(8).
           05 REAL-STORAGE-M                PIC S9(6) COMP.
           05 PRODI                         PIC X(8).
           05 PRODN                         PIC X(8).
           05 MDL                           PIC 9999.
           05 SMF-NAME                      PIC X(4).
           05 IPL-JDATE                     PIC 9(7) COMP-3.
           05 SU-SEC                        PIC S9(6) COMP.
           05 CPU-TYPE                      PIC X(6).
           05 CPU-MODEL                     PIC X(3).
           05 SYSPLEX-NAME                  PIC X(8).
           05 OS390-RELEASE                 PIC 9(6).
           05 HARDWARE-NAME                 PIC X(8).
           05 LPAR-NAME                     PIC X(8).
           05 VM-USERID                     PIC X(8).
         01 FOUR-BYTES.
           05 FULL-WORD                     PIC S9(8) COMP.
           05 PTR4      REDEFINES FULL-WORD POINTER.
           05 PL4       REDEFINES FULL-WORD PIC 9(7) COMP-3.
      /
        LINKAGE SECTION.
         01 CB1.
            05 PTR1     POINTER OCCURS 512.
         01 CVT.
            05 CVT1     POINTER OCCURS 512.
      /
       PROCEDURE DIVISION.
 PSA       SET ADDRESS OF CB1 TO NULL
 CVT       SET ADDRESS OF CVT TO PTR1(5)
           MOVE CVT(341:8) TO SYS-NAME
           MOVE CVT(857:4) TO FOUR-BYTES
           COMPUTE REAL-STORAGE-M = (FULL-WORD + 1023) / 1024
           DISPLAY ' SYSNAME='  SYS-NAME
           DISPLAY ' STOR='     REAL-STORAGE-M 'M'
CVTFIX     SET PTR4 TO ADDRESS OF CVT
           SUBTRACT 256 FROM FULL-WORD
           SET ADDRESS OF CB1 TO PTR4
           MOVE CB1(217:8) TO PRODI
           MOVE CB1(225:8) TO PRODN
           MOVE ZERO TO PL4
           MOVE CB1(251:2) TO FOUR-BYTES(1:2)
           COMPUTE MDL = PL4 / 1000
           DISPLAY ' PRODI=' PRODI
           DISPLAY ' PRODN=' PRODN
           DISPLAY ' MODEL=' MDL
 SMCA      SET ADDRESS OF CB1 TO CVT1(50)
           MOVE CB1(17:4) TO SMF-NAME
           MOVE CB1(341:4) TO FOUR-BYTES
           COMPUTE IPL-JDATE = PL4 + 1900000
           DISPLAY ' SMFSID='   SMF-NAME
           DISPLAY ' IPL='      IPL-JDATE
 RMCT      SET ADDRESS OF CB1 TO CVT1(152)
           MOVE CB1(65:4) TO FOUR-BYTES
           COMPUTE SU-SEC = 16000000 / FULL-WORD
           DISPLAY ' SPEED='   SU-SEC ' SU/S'
 HID       SET ADDRESS OF CB1 TO CVT1(268)
           MOVE CB1(27:6) TO CPU-TYPE
           MOVE CB1(33:3) TO CPU-MODEL
           DISPLAY ' CPU='     CPU-TYPE '-' CPU-MODEL
 ECVT      SET ADDRESS OF CB1 TO CVT1(36)
           MOVE CB1(9:8) TO SYSPLEX-NAME
           MOVE CB1(337:8) TO HARDWARE-NAME
           MOVE CB1(345:8) TO LPAR-NAME
           MOVE CB1(353:8) TO VM-USERID
           MOVE CB1(513:6) TO OS390-RELEASE
           DISPLAY ' SYSPLEX=' SYSPLEX-NAME
           DISPLAY ' HWNAME='  HARDWARE-NAME
           DISPLAY ' LPAR='    LPAR-NAME
           DISPLAY ' VM='      VM-USERID
           DISPLAY ' OS/390='  OS390-RELEASE
           GOBACK.
