//JOBNAME JOB (ACCT,'ROOM-NBR'),'HOTTEL SHELSORT',MSGCLASS=S,CLASS=K,
//  TIME=(,30)
//PROCLIB JCLLIB ORDER=USERID.DVL.PROC
//        SET TITLE1='XXX.ASM00020'
//OUTPUT INCLUDE MEMBER=DESTCH
//*-------------------------------------------------------------------*
//*        DISPLAY SYSTEM INFO                                        *
//*-------------------------------------------------------------------*
//STEP1    EXEC PGM=COBSYS
//STEPLIB  DD  DSN=PREFIX.DVL.BATLOAD,DISP=SHR
//SYSOUT   DD  SYSOUT=*
//*-------------------------------------------------------------------*
//*        ASSEMBLY                                                   *
//*-------------------------------------------------------------------*
//STEP02   EXEC HLASMCLG
//C.SYSLIB DD DSN=USERID.MACLIB,DISP=SHR
//         DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSUT2 DD DSN=&&SYSUT2,UNIT=SYSDA,
//           SPACE=(1700,(600,100))
//C.SYSPUNCH DD DUMMY
//C.SYSIN DD *
         TITLE 'SHELL SORT'
         PRINT NOGEN
*---------------------------------------------------------------------*
*        04 APR 1983 - CHARLES HOTTEL                                 *
*        SHELL SORT                                                   *
*                                                                     *
*        THE ART OF COMPUTER PROGRAMMING, SORTING AND SEARCHING,      *
*        2ND EDITION. PAGES 84-95.                                    *
*                                                                     *
*        RECORDS R(1), ... ,R(N) REARRANGED IN PLACE.           ).    *
*                                                                     *
*        SEQUENCE OF INCREMENTS H(T),H(T-1), ... ,H(1) USED TO CONTROL*
*        THE SORTING PROCESS WHERE H(1) = 1. PROPER CHOICE OF THESE   *
*        INCREMENTS CAN SIGNIFICANTLY DECREASE THE SORTING TIME.      *
*        THE BEST POSSIBLE SEQUENCE HAS NOT YET BEEN DETERMINED.      *
*                                                                     *
*        DESIRABLE TO SORT WITH RELATIVELY PRIME INCREMENTS.          *
*        UNWISE TO START WITH H(T) > N/3.                             *
*        SINCE THE INCREMENTS (3**K - 1) / 2 DO NOT TAKE              *
*        SUBSTANTIALLY MORE MOVES, AND SINCE THEY REQUIRE ONLY        *
*        ABOUT 5/8 AS MANY PASSES AS THE INCREMENTS OF OTHER FORMS,   *
*        THE EVIDENCE INDICATES THAT IT IS REASONABLE TO CHOOSE THE   *
*        INCREMENTS IN THE FOLLOWING WAY:                             *
*                                                                     *
*            LET H(1) = 1                                             *
*            LET H(S+1) = 3*H(S) + 1                                  *
*            STOP WITH H(T) WHEN H(T+2) GE N                          *
*                                                                     *
*        NOT STABLE, REASONABLY EFFICIENT FOR 1000 RECORDS OR LESS.   *
*---------------------------------------------------------------------*
*        27 AUG 2002 - CHARLES HOTTEL                                 *
*                                                                     *
*        UPDATED JCL TO RUN COBSYS COBOL PROGRAM WHICH DISPLAYS       *
*        SYSTEM INFORMATION. COBSYS WAS WRITTEN BY GILBERT ST. FLOUR. *
*                                                                     *
*        CHANGED TIMING ROUTINES TO DISPLAY TIMES IN MICROSECONDS     *
*        TIMING ROUTINE IS FROM RICK HEVENER'S ARTCILE "LARGE SYSTEM  *
*        INSTRUCTION TIMINGS", TECHNICAL SUPPORT AUGUST 1997.         *
*---------------------------------------------------------------------*
SHELSORT START
         STM   R14,R12,12(R13)
         BALR  R15,0
         USING *,R15
         L     R1,MAIN-16
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R12,MAIN-12
         DROP  R15
         USING MAIN,R10,R11,R12
         B     MAIN
         DC    A(SAVE,MAIN,MAIN+4096,MAIN+8192)
*---------------------------------------------------------------------*
*   MAIN LINE CODE                                                    *
*---------------------------------------------------------------------*
MAIN     EQU   *
         OPEN  (OUTFILE,OUTPUT)
         STIMER TASK,BINTVL=LONGTIME
         CPUTIMER MIC,BGNTIME
         L     R15,=A(SORTRTN)
         BAL   R14,SENTER          SORT
         CPUTIMER MIC,ENDTIME
         L     R15,=A(CALCTIME)
         BAL   R14,SENTER          CALCULATE MICROSECONDS
         L     R15,=A(PRTRTN)
         BAL   R14,SENTER          PRINT SORTED VALUES
         CLOSE (OUTFILE)
MAIN99   SR    R15,R15
         B     SEXIT
         TITLE 'COMMON SUBROUTINE ENTRY/EXIT'
*
*   COMMON SUBROUTINE ENTRY
*
SENTER   STM   R14,R12,12(R13)
         C     R13,=A(SAVEND)
         BL    SENTER1
         DC    H'0'
         DC    C'SAVE AREA OVERFLOW'
SENTER1  LR    R9,R15
         LA    R15,72(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         BR    R9
*
*   COMMON SUBROUTINE EXIT
*
SEXIT    L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'
         BR    R14
         TITLE 'REGISTER EQUATES AND USAGE'
*
*   REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9   SENTER BRANCH TO SUBROUTINE, SUBROUTINE USES AS BASE
R10      EQU   10  BASE REGISTER FOR MAIN
R11      EQU   11  BASE REGISTER FOR MAIN
R12      EQU   12  BASE REGISTER FOR MAIN
R13      EQU   13  SAVE AREA ADDRESS
R14      EQU   14  SEXIT RETURN ADDRESS
R15      EQU   15  BRANCH TO ADDRESS, SENTER COPIES TO R9
*                  RETURN CODE FROM SUBROUTINE
         TITLE 'LITERALS'
         LTORG
         TITLE 'MAIN ROUTINE DATA'
         EJECT
OUTFILE  DCB   DSORG=PS,                                               X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               BLKSIZE=27930,                                          X
               LRECL=133,                                              X
               DDNAME=PRTOUT
         DS    0D
DBLWORD  DS    D
BGNTIME  DS    D                   BEGIN TIME
ENDTIME  DS    D                   END TIME
LONGTIME DC    F'2E9'
SW       DC    X'00'
TEMP     DS    CL3
*
TIMEREC  DS    0CL133
         DC    C'1'
         DC    CL15'ELAPSED TIME = '
MSGHOLD  DS    CL15
         DC    CL14' MICROSECONDS.'
         DC    CL88' '
*
OUTREC   DS    0CL133
         DC    C' '
OUTKEY   DS    CL3
         DC    CL129' '
*
         DS    0F
H        DC    10F'0'              INCREMENTS
*
UNSORTED EQU   *
         DC    CL3'503'
         DC    CL3'087'
         DC    CL3'512'
         DC    CL3'061'
         DC    CL3'908'
         DC    CL3'170'
         DC    CL3'897'
         DC    CL3'275'
         DC    CL3'653'
         DC    CL3'426'
         DC    CL3'154'
         DC    CL3'509'
         DC    CL3'612'
         DC    CL3'677'
         DC    CL3'765'
         DC    CL3'703'
LASTUN   EQU   *-3
*
SHELSRT  CSECT
*
*   REMOTE AREAS
*
SAVE     DS    90D
SAVEND   EQU   *
         TITLE 'SHELL SORT'
SORTRTN  B     14(0,R9)
         USING SORTRTN,R9
         DC    AL1(8),CL9'SORTRTN'
*---------------------------------------------------------------------*
*        FOR T=1,2,03,04, ...                                         *
*            S=1,4,13,40, ... ,(3**K)-1)/2 WHILE S < N                *
*                H(T) <- S                                            *
*        ENDFOR                                                       *
*        FOR S=T,T-1, ... ,1                                          *
*            H <- H(S)                                                *
*            FOR J=H+1,H+2, ... ,N                                    *
*                I <- J-H                                             *
*                IF K GE K(J-H)                                       *
*                    GOTO NEXTJ.                                      *
*                K <- K(J)                                            *
* SWAP1          R(I+H) <- R(I)                                       *
*                I <- I-H                                             *
*                IF I LE ZERO                                         *
*                    GOTO SWAP2.                                      *
*                IF K < K(I)                                          *
*                    GOTO SWAP1.                                      *
* SWAP2.         R(I+H) <- R.                                         *
* NEXTJ      ENDFOR                                                   *
*        ENDFOR                                                       *
*---------------------------------------------------------------------*
         LA    R1,16               N
         L     R3,=A(H-8)          T
         LA    R5,1                S
LOOPH    ST    R5,8(R3)            H(T) <- S
         M     R4,=F'3'            S <- (3*S)+1
         LA    R5,1(R5)
         LA    R3,4(R3)            NEXT T
         CR    R5,R1               S GE N ?
         BL    LOOPH               NO
         LR    R1,R3               S <- T
         L     R2,=A(UNSORTED)     BASE FOR RECS
LOOPS    L     R5,0(R1)            H <- H(S)
         M     R4,=F'3'            DISPL H IN R5
         LCR   R4,R5               NEG DISPL IN R4
         LA    R3,0(R5,R2)         J <- H+1
LOOPJ    LA    R6,0(R4,R3)         I <- J-H
         CLC   0(3,R3),0(R6)       IS K GE K(I) ?
         BNL   NEXTJ               YES
         MVC   TEMP,0(R3)          NO, K <- K(J), R <- R(J)
SWAP1    LA    R7,0(R5,R6)         I+H
         MVC   0(3,R7),0(R6)       R(I+H) <- R(I)
         LA    R6,0(R4,R6)         I <- I-H
         CR    R6,R2               I LE ZERO ?
         BL    SWAP2               YES
         CLC   TEMP,0(R6)          K < K(I)
         BL    SWAP1               YES
SWAP2    LA    R7,0(R5,R6)         I+H
         MVC   0(3,R7),TEMP        R(I+H) <- R
NEXTJ    LA    R3,3(R3)            J <- J+1
         CL    R3,=A(LASTUN)       J > N ?
         BNH   LOOPJ               NO
         S     R1,=F'4'            NEXTS
         CL    R1,=A(H)            S < ZERO ?
         BNL   LOOPS               NO
         B     SEXIT
         LTORG
         TITLE 'CALCTIME'
CALCTIME B     14(0,R9)
         USING CALCTIME,R9
         DC    AL1(8),CL9'CALCTIME'
*---------------------------------------------------------------------*
* COMPUTE ELAPSED TIME                                                *
*---------------------------------------------------------------------*
         LM    R2,R3,BGNTIME       GET BEGIN TIME
         SL    R3,ENDTIME+4
         BC    3,CALC1
         BCTR  R2,0                BORROW
CALC1    SL    R2,ENDTIME          R2 <- ELAPSED TIME
*---------------------------------------------------------------------*
* MAKE R4 & R3 LESS THAN 2**31                                        *
* GOING INTO CALC2 THERE ARE TWO CASES:                               *
*  1. R2 IS ALREADY ZERO AND THE SLDL R2,2 BEFORE THE LOOP IS REVERSED*
*     BY THE SRDL R2,1 WITHIN THE LOOP AND BY THE SRDL R2,1           *
*     FOLLOWING THE LOOP.                                             *
*  2. R2 IS GREATER THAN ZERO. THE LOOP KEEPS SHIFTING RIGHT UNTIL R2 *
*     IS ZERO. THE SRDL R2,1 AFTER THE LOOP MAKES SURE THAT THE       *
*     LEFTMOST BIT OF R3 IS ZERO. THE VALUE IN R3 IS OVERSHIFTED TO   *
*     THE RIGHT.  SOME BITS MAY HAVE DROPPED OFF THE RIGHT END OF R3  *
*     BUT THEY ARE VERY SMALL FRACTIONS OF A MICROSECOND. THE         *
*     MULTIPLY GIVES MICROSECONDS OVERSHIFTED TO THE RIGHT. THE       *
*     SLDL R2,0(R5) EVENS OUT THE SHIFTING, GIVING MICROSECONDS.      *
*---------------------------------------------------------------------*
         LA    R6,1
         LNR   R5,R6               NBR OF SHIFTS = -1 (SET UP LOOP)
         SLDL  R2,2                RECOVER ONE IN LOOP, ONE AFTER
CALC2    AR    R5,R6               # = # + 1
         SRDL  R2,1                RECOVER 1
         LTR   R2,R2
         BNZ   CALC2
         SRDL  R2,1                NOW R2 & R3 LESS THAN 2**31
*********M     R2,=F'1E6'          CONVERT MICROSEC TO PICOSEC
*********SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 PICOSEC
*********SRDL  R2,12               BIT 63 = 1 PICOSEC
         SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 MICROSEC
         SRDL  R2,12               BIT 63 = 1 MICROSEC
*---------------------------------------------------------------------*
* DISPLAY RESULTS                                                     *
*---------------------------------------------------------------------*
         CVD   R3,DBLWORD
         UNPK  MSGHOLD,DBLWORD
         OI    MSGHOLD+14,X'F0'
         PUT   OUTFILE,TIMEREC
         B     SEXIT
         LTORG
         TITLE 'PRTRTN'
PRTRTN   B     14(0,R9)
         USING PRTRTN,R9
         DC    AL1(8),CL9'PRTRTN'
         LA    R2,16
         L     R3,=A(UNSORTED)
         LA    R4,56               NUMBER OF RECS PER PAGE
         LA    R5,57               COUNT RECS OUTPUT
PRTLOOP2 MVC   OUTKEY,0(R3)        PLACE IN OUTPUT REC
         LA    R5,1(R5)            COUNT REC
         CR    R5,R4
         BH    NEWPAGE             MORE THAN 56
         PUT   OUTFILE,OUTREC      LESS THAN 56
         B     PRTST
NEWPAGE  MVI   OUTREC,C'1'         NEW PAGE
         PUT   OUTFILE,OUTREC
         MVI   OUTREC,C' '         SINGLE SPACE
         SR    R5,R5
PRTST    LA    R3,3(R3)            NEXT SORTED REC
         BCT   R2,PRTLOOP2         BRANCH IF MORE RECS?
         B     SEXIT
         LTORG
         END
//G.PRTOUT   DD SYSOUT=*
//G.SYSOUT   DD SYSOUT=*
//G.SYSUDUMP DD SYSOUT=*
