//JOBNAME JOB (ACCT,'ROOM-NBR'),'HOTTEL QUIKSORT',MSGCLASS=S,CLASS=K,
//  TIME=(,30)
//PROCLIB JCLLIB ORDER=USERID.DVL.PROC
//        SET TITLE1='XXX.ASM00040'
//OUTPUT INCLUDE MEMBER=DESTCH
//*-------------------------------------------------------------------*
//*        DISPLAY SYSTEM INFO                                        *
//*-------------------------------------------------------------------*
//STEP1    EXEC PGM=COBSYS
//STEPLIB  DD  DSN=PREFIX.DVL.BATLOAD,DISP=SHR
//SYSOUT   DD  SYSOUT=*
//*-------------------------------------------------------------------*
//*        ASSEMBLY                                                   *
//*-------------------------------------------------------------------*
//STEP02   EXEC HLASMCLG
//C.SYSLIB DD DSN=USERID.MACLIB,DISP=SHR
//         DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSUT2 DD DSN=&&SYSUT2,UNIT=SYSDA,
//           SPACE=(1700,(600,100))
//C.SYSPUNCH DD DUMMY
//C.SYSIN DD *
         TITLE 'MEDIAN OF THREE QUICKSORT (PARTITION EXCHANGE)'
         PRINT NOGEN
*---------------------------------------------------------------------*
*        13 APR 1983 - CHARLES HOTTEL                                 *
*        QUICKSORT (PARTITION EXCHANGE)                               *
*                                                                     *
*        THE ART OF COMPUTER PROGRAMMING, SORTING AND SEARCHING,      *
*        2ND EDITION. PAGES 114-123                                   *
*                                                                     *
*        TAKE ONE RECORD AND MOVE IT TO ITS FINAL POSITION S. WHILE   *
*        DETERMINING THIS FINAL POSITION REARRANGE THE RECORDS SO     *
*        NONE WITH GREATER KEYS ARE TO THE LEFT OF POSITION S, AND    *
*        NONE WITH SMALLER KEYS ARE TO THE RIGHT. THE FILE IS THUS    *
*        PARTITIONED SO THAT THE ORIGINAL PROBLEM IS REDUCED TO TWO   *
*        SIMPLER PROBLEMS, NAMELY TO SORT R(1), ... .R(S-1) AND       *
*        (INDEPENDENTLY) R(S+1), ... ,R(N).                           *
*                                                                     *
*        M=9 SEEMS SLIGHTLY FASTER THAN M=8.                          *
*                                                                     *
*        VERY SLOW IF ORIGINAL FILE ALREADY IN ORDER.                 *
*        NOT STABLE                                                   *
*---------------------------------------------------------------------*
*        TESTING WORST CASE: ALL RECORDS ALREADY IN ORDER             *
*---------------------------------------------------------------------*
*        28 AUG 2002 - CHARLES HOTTEL                                 *
*                                                                     *
*        UPDATED JCL TO RUN COBSYS COBOL PROGRAM WHICH DISPLAYS       *
*        SYSTEM INFORMATION. COBSYS WAS WRITTEN BY GILBERT ST. FLOUR. *
*                                                                     *
*        CHANGED TIMING ROUTINES TO DISPLAY TIMES IN MICROSECONDS     *
*        TIMING ROUTINE IS FROM RICK HEVENER'S ARTCILE "LARGE SYSTEM  *
*        INSTRUCTION TIMINGS", TECHNICAL SUPPORT AUGUST 1997.         *
*---------------------------------------------------------------------*
QUIKSORT START
         STM   R14,R12,12(R13)
         BALR  R15,0
         USING *,R15
         L     R1,MAIN-12
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R12,MAIN-8
         DROP  R15
         USING MAIN,R10,R11
         B     MAIN
         DC    A(SAVE,MAIN,MAIN+4096)
*---------------------------------------------------------------------*
*   MAIN LINE CODE                                                    *
*---------------------------------------------------------------------*
MAIN     EQU   *
         OPEN  (OUTFILE,OUTPUT)
         STIMER TASK,BINTVL=LONGTIME
         CPUTIMER MIC,BGNTIME
         L     R15,=A(SORTRTN)
         BAL   R14,SENTER          SORT
         CPUTIMER MIC,ENDTIME
         L     R15,=A(CALCTIME)
         BAL   R14,SENTER          CALCULATE MICROSECONDS
         L     R15,=A(PRTRTN)
         BAL   R14,SENTER          PRINT SORTED VALUES
         CLOSE (OUTFILE)
MAIN99   SR    R15,R15
         B     SEXIT
         TITLE 'COMMON SUBROUTINE ENTRY/EXIT'
*
*   COMMON SUBROUTINE ENTRY
*
SENTER   STM   R14,R12,12(R13)
         C     R13,=A(SAVEND)
         BL    SENTER1
         DC    H'0'
         DC    C'SAVE AREA OVERFLOW'
SENTER1  LR    R9,R15
         LA    R15,72(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         BR    R9
*
*   COMMON SUBROUTINE EXIT
*
SEXIT    L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'
         BR    R14
         TITLE 'REGISTER EQUATES AND USAGE'
*
*   REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9   SENTER BRANCH TO SUBROUTINE, SUBROUTINE USES AS BASE
R10      EQU   10  BASE REGISTER FOR MAIN
R11      EQU   11  BASE REGISTER FOR MAIN
R12      EQU   12  BASE REGISTER FOR MAIN
R13      EQU   13  SAVE AREA ADDRESS
R14      EQU   14  SEXIT RETURN ADDRESS
R15      EQU   15  BRANCH TO ADDRESS, SENTER COPIES TO R9
*                  RETURN CODE FROM SUBROUTINE
         TITLE 'LITERALS'
         LTORG
         TITLE 'MAIN ROUTINE DATA'
         EJECT
OUTFILE  DCB   DSORG=PS,                                               X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               BLKSIZE=27930,                                          X
               LRECL=133,                                              X
               DDNAME=PRTOUT
         DS    0D
DBLWORD  DS    D
BGNTIME  DS    D                   BEGIN TIME
ENDTIME  DS    D                   END TIME
LONGTIME DC    F'2E9'
SW       DC    X'00'
TEMP     DS    CL3
*
TIMEREC  DS    0CL133
         DC    C'1'
         DC    CL15'ELAPSED TIME = '
MSGHOLD  DS    CL15
         DC    CL14' MICROSECONDS.'
         DC    CL88' '
*
OUTREC   DS    0CL133
         DC    C' '
OUTKEY   DS    CL3
         DC    CL129' '
*
         DC    XL3'000000'
UNSORTED EQU   *
         DC    CL3'061'
         DC    CL3'087'
         DC    CL3'154'
         DC    CL3'170'
         DC    CL3'275'
         DC    CL3'426'
         DC    CL3'503'
         DC    CL3'509'
         DC    CL3'512'
         DC    CL3'612'
         DC    CL3'653'
         DC    CL3'677'
         DC    CL3'703'
         DC    CL3'765'
         DC    CL3'897'
         DC    CL3'908'
LASTUN   EQU   *-3
         DC    XL3'FFFFFF'
*
         DS    0F
STACK    EQU   *
         DC    20F'0'              2*LN(N) FULLWORDS
*
QUIKSRT  CSECT
*
*   REMOTE AREAS
*
SAVE     DS    90D
SAVEND   EQU   *
         TITLE 'MEDIAN OF THREE QUICKSORT (PARTITION EXCHANGE)'
SORTRTN  B     14(0,R9)
         USING SORTRTN,R9
         DC    AL1(8),CL9'SORTRTN'
         LR    R12,R9
         DROP  R9
         USING SORTRTN,R12
         L     R6,=A(STACK)        SET STACK EMPTY
         L     R2,=A(UNSORTED)     L <- 1
         L     R3,=A(LASTUN)       R <- N
NEWSTAGE LR    R9,R3               R9 <- R*3
         SR    R9,R2               R9 <- (R-L)*3
         SR    R8,R8
         D     R8,=F'6'            R9 <- ((R-L)*3) / 3*2
         M     R8,=F'3'            R9 <- DISP Q
         LA    R4,0(R9,R2)         R4 <- PTR K(Q)
         CLC   0(3,R2),0(R3)       K(L) < K(R)
         BL    H1                  YES
         CLC   0(3,R2),0(R4)       K(L) LE K(Q)
         BNH   H3                  YES
         CLC   0(3,R3),0(R4)       K(R) > K(Q)
         BH    H4                  YES
**********                         K(R) LE K(Q) < K(L)
         XC    0(3,R2),0(R3)       INTERCHANGE K(L) AND K(R)
         XC    0(3,R3),0(R2)
         XC    0(3,R2),0(R3)
H0       MVC   TEMP,0(R4)          TEMP <- K(Q)
         MVC   0(3,R4),3(R2)       K(Q) <- K(L+1)
         MVC   3(3,R2),TEMP        K(L+1) <- TEMP
         B     H6
**********                         K(Q) < K(R) LE K(L)
H4       MVC   TEMP,0(R3)          TEMP <- K(R)
         MVC   0(3,R3),0(R2)       K(R) <- K(L)
         MVC   0(3,R2),0(R4)       K(L) <- K(Q)
         MVC   0(3,R4),3(R2)       K(Q) <- K(L+1)
         MVC   3(3,R2),TEMP        K(L+1) <- TEMP
         B     H6
**********                         K(R) LE K(L) LE K(Q)
H3       MVC   TEMP,0(R2)          TEMP <- K(L)
         MVC   0(3,R2),0(R3)       K(L) <- K(R)
         MVC   0(3,R3),0(R4)       K(R) <- K(Q)
         MVC   0(3,R4),3(R2)       K(Q) <- K(L+1)
         MVC   3(3,R2),TEMP        K(L+1) <- TEMP
         B     H6
H1       CLC   0(3,R2),0(R4)       K(L) GE K(Q)
         BNL   H5                  YES
         CLC   0(3,R3),0(R4)       K(R) > K(Q)
         BH    H0                  YES K(L) < K(Q) < K(R)
**********                         K(L) < K(R) LE K(Q)
         MVC   TEMP,0(R3)          TEMP <- K(R)
         MVC   0(3,R3),0(R4)       K(R) <- K(Q)
         MVC   0(3,R4),3(R2)       K(Q) <- K(L+1)
         MVC   3(3,R2),TEMP        K(L+1) <- TEMP
         B     H6
**********                         K(Q) LE K(L) < K(R)
H5       MVC   TEMP,0(R2)          TEMP <- K(L)
         MVC   0(3,R2),0(R4)       K(L) <- K(Q)
         MVC   0(3,R4),3(R2)       K(Q) <- K(L+1)
         MVC   3(3,R2),TEMP        K(L+1) <- TEMP
H6       LA    R4,6(R2)            I <- L+2
         LR    R5,R3               L <- R
         B     COMPARE1
EXCHANGE XC    0(3,R4),0(R5)       INTERCHANGE
         XC    0(3,R5),0(R4)         K(I) AND K(J)
         XC    0(3,R4),0(R5)
NEXTI    LA    R4,3(R4)            I <- I+1
COMPARE1 CLC   TEMP,0(R4)          K > K(I) ?
         BH    NEXTI               YES
NEXTJ    S     R5,=F'3'            J <- J-1
         CLC   TEMP,0(R5)          K < K(J) ?
         BL    NEXTJ               YES
TESTIJ   CR    R5,R4               J > I?
         BH    EXCHANGE            YES
         MVC   3(3,R2),0(R5)       K(L+1) <- K(J)
         MVC   0(3,R5),TEMP        K(J) <- K
STACKIT  LR    R9,R3               R9 <- R
         SR    R9,R5               R9 <- (R-J)*3
         S     R9,=F'27'           R9 <- R-J-M  (M=9)
         LR    R15,R5              R15 <- J
         SR    R15,R2              R15 <- (J-L)*3
         S     R15,=F'27'          R15 <- J-L-M
         LR    R8,R9
         SR    R8,R15
         BC    10,NEXTEST          JUMP IF R-J GE J-L
         LTR   R15,R15
         BNP   UNSTACK             JUMP IF M GE J-L > R-J
         LTR   R9,R9
         BNP   NEXTR               J-L > M GE R-J
         LA    R6,8(R6)            NOW J-L > R-J > M
         ST    R2,0(R6)            STACK <- L
         LR    R14,R5              R14 <- J
         S     R14,=F'3'           R14 <- J-1
         ST    R14,4(R6)           STACK <- J-1
NEXTL    LA    R2,3(R5)            L <- J+1
         B     NEWSTAGE
NEXTEST  LTR   R9,R9
         BNP   UNSTACK             M GE R-J GE J-L
         LTR   R15,R15
         BNP   NEXTL               R-J > M GE J-L
         LA    R6,8(R6)
         ST    R3,4(R6)            STACK <- R
         LA    R14,3(R5)           R14 <- J+1
         ST    R14,0(R6)           STACK <- J+1
NEXTR    LR    R3,R5
         S     R3,=F'3'            R <- J-1
         B     NEWSTAGE
UNSTACK  L     R2,0(R6)            L <- STACK
         L     R3,4(R6)            R <- STACK
         S     R6,=F'8'
         CL    R6,=A(STACK)        WAS STACK EMPTY?
         BNL   NEWSTAGE            NO
         LA    R1,15               STRAIGHT INSERTION SORT
         L     R2,=A(UNSORTED+3)   J
SAVEKEY  MVC   TEMP,0(R2)          K <- K(J)
         LR    R3,R2
         S     R3,=F'3'            I <- J-1
         CLC   TEMP,0(R3)          K GE K(J-1)
         BNL   JNEXT               NO
MOVEKEY  MVC   3(3,R3),0(R3)       K(I+1) <- K(I)
         S     R3,=F'3'            I <- I-1
         CLC   TEMP,0(R3)          K GE K(I)
         BL    MOVEKEY             NO
         MVC   3(3,R3),TEMP        YES, K(I+1) <- K
JNEXT    LA    R2,3(R2)            J <- J+1
         BCT   R1,SAVEKEY
         B     SEXIT
         LTORG
         DROP  R12
         TITLE 'CALCTIME'
CALCTIME B     14(0,R9)
         USING CALCTIME,R9
         DC    AL1(8),CL9'CALCTIME'
*---------------------------------------------------------------------*
* COMPUTE ELAPSED TIME                                                *
*---------------------------------------------------------------------*
         LM    R2,R3,BGNTIME       GET BEGIN TIME
         SL    R3,ENDTIME+4
         BC    3,CALC1
         BCTR  R2,0                BORROW
CALC1    SL    R2,ENDTIME          R2 <- ELAPSED TIME
*---------------------------------------------------------------------*
* MAKE R4 & R3 LESS THAN 2**31                                        *
* GOING INTO CALC2 THERE ARE TWO CASES:                               *
*  1. R2 IS ALREADY ZERO AND THE SLDL R2,2 BEFORE THE LOOP IS REVERSED*
*     BY THE SRDL R2,1 WITHIN THE LOOP AND BY THE SRDL R2,1           *
*     FOLLOWING THE LOOP.                                             *
*  2. R2 IS GREATER THAN ZERO. THE LOOP KEEPS SHIFTING RIGHT UNTIL R2 *
*     IS ZERO. THE SRDL R2,1 AFTER THE LOOP MAKES SURE THAT THE       *
*     LEFTMOST BIT OF R3 IS ZERO. THE VALUE IN R3 IS OVERSHIFTED TO   *
*     THE RIGHT.  SOME BITS MAY HAVE DROPPED OFF THE RIGHT END OF R3  *
*     BUT THEY ARE VERY SMALL FRACTIONS OF A MICROSECOND. THE         *
*     MULTIPLY GIVES MICROSECONDS OVERSHIFTED TO THE RIGHT. THE       *
*     SLDL R2,0(R5) EVENS OUT THE SHIFTING, GIVING MICROSECONDS.      *
*---------------------------------------------------------------------*
         LA    R6,1
         LNR   R5,R6               NBR OF SHIFTS = -1 (SET UP LOOP)
         SLDL  R2,2                RECOVER ONE IN LOOP, ONE AFTER
CALC2    AR    R5,R6               # = # + 1
         SRDL  R2,1                RECOVER 1
         LTR   R2,R2
         BNZ   CALC2
         SRDL  R2,1                NOW R2 & R3 LESS THAN 2**31
*********M     R2,=F'1E6'          CONVERT MICROSEC TO PICOSEC
*********SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 PICOSEC
*********SRDL  R2,12               BIT 63 = 1 PICOSEC
         SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 MICROSEC
         SRDL  R2,12               BIT 63 = 1 MICROSEC
*---------------------------------------------------------------------*
* DISPLAY RESULTS                                                     *
*---------------------------------------------------------------------*
         CVD   R3,DBLWORD
         UNPK  MSGHOLD,DBLWORD
         OI    MSGHOLD+14,X'F0'
         PUT   OUTFILE,TIMEREC
         B     SEXIT
         LTORG
         TITLE 'PRTRTN'
PRTRTN   B     14(0,R9)
         USING PRTRTN,R9
         DC    AL1(8),CL9'PRTRTN'
         LA    R2,16
         L     R3,=A(UNSORTED)     HEAD OF LIST
         LA    R4,56               NUMBER OF RECS PER PAGE
         LA    R5,57               COUNT RECS OUTPUT
PRTLOOP2 MVC   OUTKEY,0(R3)        PLACE IN OUTPUT REC
         LA    R5,1(R5)            COUNT REC
         CR    R5,R4
         BH    NEWPAGE             MORE THAN 56
         PUT   OUTFILE,OUTREC      LESS THAN 56
         B     PRTST
NEWPAGE  MVI   OUTREC,C'1'         NEW PAGE
         PUT   OUTFILE,OUTREC
         MVI   OUTREC,C' '         SINGLE SPACE
         SR    R5,R5
PRTST    LA    R3,3(R3)            NEXT SORTED REC
         BCT   R2,PRTLOOP2
         B     SEXIT
         LTORG
         END
//G.PRTOUT   DD SYSOUT=*
//G.SYSOUT   DD SYSOUT=*
//G.SYSUDUMP DD SYSOUT=*
