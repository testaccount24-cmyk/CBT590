//JOBNAME JOB (ACCT,'ROOM-NBR'),'HOTTEL HEAPSORT',MSGCLASS=S,CLASS=K,
//  TIME=(,30)
//PROCLIB JCLLIB ORDER=USERID.DVL.PROC
//        SET TITLE1='XXX.ASM00047'
//OUTPUT INCLUDE MEMBER=DESTCH
//*-------------------------------------------------------------------*
//*        DISPLAY SYSTEM INFO                                        *
//*-------------------------------------------------------------------*
//STEP1    EXEC PGM=COBSYS
//STEPLIB  DD  DSN=PREFIX.DVL.BATLOAD,DISP=SHR
//SYSOUT   DD  SYSOUT=*
//*-------------------------------------------------------------------*
//*        ASSEMBLY                                                   *
//*-------------------------------------------------------------------*
//STEP02   EXEC HLASMCLG
//C.SYSLIB DD DSN=USERID.MACLIB,DISP=SHR
//         DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSUT2 DD DSN=&&SYSUT2,UNIT=SYSDA,
//           SPACE=(1700,(600,100))
//C.SYSPUNCH DD DUMMY
//C.SYSIN DD *
         TITLE 'HEAPSORT'
         PRINT NOGEN
*---------------------------------------------------------------------*
*        22 APR 1983 - CHARLES HOTTEL                                 *
*        HEAPSORT
*                                                                     *
*        THE ART OF COMPUTER PROGRAMMING, SORTING AND SEARCHING,      *
*        2ND EDITION. PAGES 145-149                                   *
*                                                                     *
*        NOT STABLE                                                   *
*---------------------------------------------------------------------*
*        05 SEP 2002 - CHARLES HOTTEL                                 *
*                                                                     *
*        UPDATED JCL TO RUN COBSYS COBOL PROGRAM WHICH DISPLAYS       *
*        SYSTEM INFORMATION. COBSYS WAS WRITTEN BY GILBERT ST. FLOUR. *
*                                                                     *
*        CHANGED TIMING ROUTINES TO DISPLAY TIMES IN MICROSECONDS     *
*        TIMING ROUTINE IS FROM RICK HEVENER'S ARTCILE "LARGE SYSTEM  *
*        INSTRUCTION TIMINGS", TECHNICAL SUPPORT AUGUST 1997.         *
*---------------------------------------------------------------------*
HEAPSORT START
         STM   R14,R12,12(R13)
         BALR  R15,0
         USING *,R15
         L     R1,MAIN-16
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R12,MAIN-12
         DROP  R15
         USING MAIN,R10,R11,R12
         B     MAIN
         DC    A(SAVE,MAIN,MAIN+4096,MAIN+8192)
*---------------------------------------------------------------------*
*   MAIN LINE CODE                                                    *
*---------------------------------------------------------------------*
MAIN     EQU   *
         OPEN  (OUTFILE,OUTPUT)
         STIMER TASK,BINTVL=LONGTIME
         CPUTIMER MIC,BGNTIME
         L     R15,=A(SORTRTN)
         BAL   R14,SENTER          SORT
         CPUTIMER MIC,ENDTIME
         L     R15,=A(CALCTIME)
         BAL   R14,SENTER          CALCULATE MICROSECONDS
         L     R15,=A(PRTRTN)
         BAL   R14,SENTER          PRINT SORTED VALUES
         CLOSE (OUTFILE)
MAIN99   SR    R15,R15
         B     SEXIT
         TITLE 'COMMON SUBROUTINE ENTRY/EXIT'
*
*   COMMON SUBROUTINE ENTRY
*
SENTER   STM   R14,R12,12(R13)
         C     R13,=A(SAVEND)
         BL    SENTER1
         DC    H'0'
         DC    C'SAVE AREA OVERFLOW'
SENTER1  LR    R9,R15
         LA    R15,72(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         BR    R9
*
*   COMMON SUBROUTINE EXIT
*
SEXIT    L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'
         BR    R14
         TITLE 'REGISTER EQUATES AND USAGE'
*
*   REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9   SENTER BRANCH TO SUBROUTINE, SUBROUTINE USES AS BASE
R10      EQU   10  BASE REGISTER FOR MAIN
R11      EQU   11  BASE REGISTER FOR MAIN
R12      EQU   12  BASE REGISTER FOR MAIN
R13      EQU   13  SAVE AREA ADDRESS
R14      EQU   14  SEXIT RETURN ADDRESS
R15      EQU   15  BRANCH TO ADDRESS, SENTER COPIES TO R9
*                  RETURN CODE FROM SUBROUTINE
         TITLE 'LITERALS'
         LTORG
         TITLE 'MAIN ROUTINE DATA'
         EJECT
OUTFILE  DCB   DSORG=PS,                                               X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               BLKSIZE=27930,                                          X
               LRECL=133,                                              X
               DDNAME=PRTOUT
         DS    0D
DBLWORD  DS    D
BGNTIME  DS    D                   BEGIN TIME
ENDTIME  DS    D                   END TIME
LONGTIME DC    F'2E9'
SW       DC    X'00'
TESTBIT  DC    X'00'
TEMP     DS    CL3
*
TIMEREC  DS    0CL133
         DC    C'1'
         DC    CL15'ELAPSED TIME = '
MSGHOLD  DS    CL15
         DC    CL14' MICROSECONDS.'
         DC    CL88' '
*
OUTREC   DS    0CL133
         DC    C' '
OUTKEY   DS    CL3
         DC    CL129' '
*
         DC    XL3'000000'
UNSORTED DC    CL3'503'
         DC    CL3'087'
         DC    CL3'512'
         DC    CL3'061'
         DC    CL3'908'
         DC    CL3'170'
         DC    CL3'897'
         DC    CL3'275'
         DC    CL3'653'
         DC    CL3'426'
         DC    CL3'154'
         DC    CL3'509'
         DC    CL3'612'
         DC    CL3'677'
         DC    CL3'765'
         DC    CL3'703'
LASTUN   EQU   *-3
         DC    XL3'FFFFFF'
*
HEAPSRT  CSECT
*
*   REMOTE AREAS
*
SAVE     DS    90D
SAVEND   EQU   *
         TITLE 'HEAPSORT'
SORTRTN  B     14(0,R9)
         USING SORTRTN,R9
         DC    AL1(8),CL9'SORTRTN'
         LR    R15,R9
         DROP  R9
         USING SORTRTN,R15
         LA    R1,8                L <- FLOOR(N/2)
         LA    R2,15               R <- N-1
DECL     BCTR  R1,0                L <- L-1
         LR    R9,R1               R9 <- L
         M     R8,=F'3'            R9 <- L*3
         A     R9,=A(UNSORTED)     R9 <- PTR K(L)
         MVC   TEMP,0(R9)          K <- K(L)
SIFTUP   LA    R4,1(R1)            J <- L
         LR    R5,R2               R5 <- R
         SR    R5,R1               R5 <- R-J
         B     ADVDOWN
COMPARE  LR    R7,R4               R7 <- J
         BCTR  R7,0
         M     R6,=F'3'            R7 <- J*3
         A     R7,=A(UNSORTED)     R7 <- PTR K(J)
         CLC   0(3,R7),3(R7)       K(J) GE K(J+1) ?
         BH    MOVEUP              YES
         LA    R4,1(R4)            J <- J + 1
         BCTR  R5,0                R5 <- R5-1
MOVEUP   LR    R9,R3               R9 <- I
         BCTR  R9,0
         M     R8,=F'3'            R9 <- I*3
         A     R9,=A(UNSORTED)     R9 <- PTR K(I)
         LR    R7,R4               R7 <- J
         BCTR  R7,0
         M     R6,=F'3'            R7 <- J*3
         A     R7,=A(UNSORTED)     R7 <- PTR K(J)
         MVC   0(3,R9),0(R7)       R(I) <- R(J)
ADVDOWN  LR    R3,R4
         SR    R5,R4               R5 <- R5 - J
         SLL   R4,1                J <- J+J
         LTR   R5,R5
         BP    COMPARE             J < R
         BZ    MOVEUP              J=R
NEWIJ    LR    R4,R3               J <- I
         LR    R7,R4               R7 <- J
         BCTR  R7,0
         M     R6,=F'3'            R7 <- J*3
         A     R7,=A(UNSORTED)     R7 <- PTR K(J)
         SRL   R3,1                I <- FLOOR(J/2)
         LR    R9,R3               R9 <- I
         BCTR  R9,0
         M     R8,=F'3'            R9 <- I*3
         A     R9,=A(UNSORTED)     R9 <- PTR K(I)
STORETST CLC   TEMP,0(R9)          K LE K(I) ?
         BNH   MOVEKJ              YES
         LA    R14,1(R1)           R14 <- L
         CR    R4,R14              J NE L ?
         BNE   MOVEIJ              YES
MOVEKJ   MVC   0(3,R7),TEMP        K(J) <- K
         B     DECLR
MOVEIJ   MVC   0(3,R7),0(R9)       K(J) <- K(I)
         B     NEWIJ
DECLR    LTR   R1,R1
         BP    DECL
         LR    R7,R2               R7 <- R
         BCTR  R7,0
         M     R6,=F'3'            R*3
         A     R7,=A(UNSORTED+3)   PTR K(R)
         MVC   TEMP,0(R7)          K <- K(R)
         MVC   0(3,R7),UNSORTED    K(R) <- K(1)
         S     R2,=F'1'            R <- R-1
         BP    SIFTUP
         MVC   UNSORTED(3),TEMP    K(1) <- K
         B     SEXIT
         LTORG
         DROP  R15
         TITLE 'CALCTIME'
CALCTIME B     14(0,R9)
         USING CALCTIME,R9
         DC    AL1(8),CL9'CALCTIME'
*---------------------------------------------------------------------*
* COMPUTE ELAPSED TIME                                                *
*---------------------------------------------------------------------*
         LM    R2,R3,BGNTIME       GET BEGIN TIME
         SL    R3,ENDTIME+4
         BC    3,CALC1
         BCTR  R2,0                BORROW
CALC1    SL    R2,ENDTIME          R2 <- ELAPSED TIME
*---------------------------------------------------------------------*
* MAKE R4 & R3 LESS THAN 2**31                                        *
* GOING INTO CALC2 THERE ARE TWO CASES:                               *
*  1. R2 IS ALREADY ZERO AND THE SLDL R2,2 BEFORE THE LOOP IS REVERSED*
*     BY THE SRDL R2,1 WITHIN THE LOOP AND BY THE SRDL R2,1           *
*     FOLLOWING THE LOOP.                                             *
*  2. R2 IS GREATER THAN ZERO. THE LOOP KEEPS SHIFTING RIGHT UNTIL R2 *
*     IS ZERO. THE SRDL R2,1 AFTER THE LOOP MAKES SURE THAT THE       *
*     LEFTMOST BIT OF R3 IS ZERO. THE VALUE IN R3 IS OVERSHIFTED TO   *
*     THE RIGHT.  SOME BITS MAY HAVE DROPPED OFF THE RIGHT END OF R3  *
*     BUT THEY ARE VERY SMALL FRACTIONS OF A MICROSECOND. THE         *
*     MULTIPLY GIVES MICROSECONDS OVERSHIFTED TO THE RIGHT. THE       *
*     SLDL R2,0(R5) EVENS OUT THE SHIFTING, GIVING MICROSECONDS.      *
*---------------------------------------------------------------------*
         LA    R6,1
         LNR   R5,R6               NBR OF SHIFTS = -1 (SET UP LOOP)
         SLDL  R2,2                RECOVER ONE IN LOOP, ONE AFTER
CALC2    AR    R5,R6               # = # + 1
         SRDL  R2,1                RECOVER 1
         LTR   R2,R2
         BNZ   CALC2
         SRDL  R2,1                NOW R2 & R3 LESS THAN 2**31
*********M     R2,=F'1E6'          CONVERT MICROSEC TO PICOSEC
*********SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 PICOSEC
*********SRDL  R2,12               BIT 63 = 1 PICOSEC
         SLDL  R2,0(R5)            RESTORE SHIFTS, BIT 51 = 1 MICROSEC
         SRDL  R2,12               BIT 63 = 1 MICROSEC
*---------------------------------------------------------------------*
* DISPLAY RESULTS                                                     *
*---------------------------------------------------------------------*
         CVD   R3,DBLWORD
         UNPK  MSGHOLD,DBLWORD
         OI    MSGHOLD+14,X'F0'
         PUT   OUTFILE,TIMEREC
         B     SEXIT
         LTORG
         TITLE 'PRTRTN'
PRTRTN   B     14(0,R9)
         USING PRTRTN,R9
         DC    AL1(8),CL9'PRTRTN'
         LA    R2,16
         L     R3,=A(UNSORTED)     HEAD OF LIST
         LA    R4,56               NUMBER OF RECS PER PAGE
         LA    R5,57               COUNT RECS OUTPUT
PRTLOOP2 MVC   OUTKEY,0(R3)        PLACE IN OUTPUT REC
         LA    R5,1(R5)            COUNT REC
         CR    R5,R4
         BH    NEWPAGE             MORE THAN 56
         PUT   OUTFILE,OUTREC      LESS THAN 56
         B     PRTST
NEWPAGE  MVI   OUTREC,C'1'         NEW PAGE
         PUT   OUTFILE,OUTREC
         MVI   OUTREC,C' '         SINGLE SPACE
         SR    R5,R5
PRTST    LA    R3,3(R3)          NEXT SORTED REC
         BCT   R2,PRTLOOP2
         B     SEXIT
         LTORG
         END
//G.PRTOUT   DD SYSOUT=*
//G.SYSOUT   DD SYSOUT=*
//G.SYSUDUMP DD SYSOUT=*
